# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=ppc-e2fs
pkgver=1.47.0
pkgrel=1
pkgdesc='An open source, freely available read/write NTFS driver '
arch=('any')
url='https://git.kernel.org/pub/scm/fs/ext2/e2fsprogs.git'
license=('GPL')
options=(!strip libtool staticlibs)
groups=('ppc-portlibs')

source=(
  "https://git.kernel.org/pub/scm/fs/ext2/e2fsprogs.git/snapshot/e2fsprogs-${pkgver}.tar.gz"
)

sha256sums=(
  '74c8ea97c73294edc6c11dc5e7fbb4324f86c28efd66ad0ba50be4eec8a48be2'
)

prepare() {

  sed -i 's#tst_uuid uuid_time##' e2fsprogs-${pkgver}/lib/uuid/Makefile.in
  sed -i 's,#include <arpa/inet.h>,,' e2fsprogs-${pkgver}/lib/ext2fs/jfs_compat.h

}

build() {

  cd e2fsprogs-${pkgver}

  source /opt/devkitpro/ppcvars.sh


  CPPFLAGS=-I/opt/devkitpro/libogc/include ./configure --prefix="${PORTLIBS_PREFIX}" \
    --host=powerpc-eabi \
    --disable-tls \
    --disable-uuidd \
    --disable-mmp \
    --disable-tdb \
    --disable-bmap-stats \
    --disable-fuse2fs

  make libs
}

package() {

  cd e2fsprogs-${pkgver}

  source /opt/devkitpro/ppcvars.sh

  make install-libs DESTDIR=${pkgdir}

  # remove useless stuff
  rm -fr "$pkgdir"${PORTLIBS_PREFIX}/share
}
