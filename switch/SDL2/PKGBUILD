# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=switch-sdl2
pkgver=2.28.5
pkgrel=1
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Nintendo Switch port)"
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
depends=('switch-mesa' 'libnx')
source=("${url}/release/SDL2-${pkgver}.tar.gz" "SDL2-${pkgver}.patch")
groups=('switch-portlibs' 'switch-sdl2')

build() {
  cd SDL2-$pkgver

  source /opt/devkitpro/switchvars.sh

  patch -p1 -i $srcdir/SDL2-${pkgver}.patch

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
  --disable-shared --enable-static \
  --enable-audio \
  --enable-joystick \
  --disable-power \
  --disable-filesystem \
  --enable-cpuinfo \
  --enable-pthreads \
  --enable-timers \
  --enable-video

  make
}

package() {
  cd SDL2-$pkgver

  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

sha256sums=('332cb37d0be20cb9541739c61f79bae5a477427d79ae85e352089afdaf6666e4'
            '300e902228091c42d2bfc4833b21334b552d2fda38589c826bf99751da4ba00d')
